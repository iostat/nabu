project(":core") {
    configurations {
        // a transitive dependency of kafka depends on this shitty ancient library
        // which makes ElasticSearch detect a jar hell
        // which makes everyone sad.
        all*.exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
        all*.exclude(group: 'log4j', module:'log4j')
    }

    dependencies {
        testCompile(group: 'junit', name: 'junit', version: '4.11')

        // dependencies are like heroin, you only get the most out of it when you inject
        compile(group: 'com.google.inject', name: 'guice', version: '4.0') {
            // preemptive avoidance of jar hell due to multiple versions of guava in classpath
            // guice depends on 18.0, but the latest is 19.0.
            // we may end up using it anyway (see next dependency)
            exclude group: 'com.google.guava', module: 'guava'
        }

        // include Guice deps grapher (for debugging)
        compile(group: 'com.google.inject.extensions', name: 'guice-grapher', version: '4.0')

        // slf4j log4j appenders
        compile(group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.5')
        compile(group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.5')
        // Log4J 1.x bridge since theres a subdependency of Kafka that wants it
        // (bet it doesnt even use it...)
        compile(group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.5')

        // yaml support
        compile(group: 'org.yaml', name: 'snakeyaml', version: '1.16')

        // duh
        compile(group: 'org.elasticsearch', name: 'elasticsearch', version: '2.1.0') {
            // ElasticSearch has a vendored version of Guice, which, like the one we're using, also has
            // a dependency on Guava.
            exclude group: 'com.google.guava', module: 'guava'
        }

        // for ES's JNA sorcery
        // actually serves no purpose other than shutting ES up on startup
        compile(group: 'net.java.dev.jna', name: 'jna', version: '4.2.1')

        // also duh
        compile(group: 'org.apache.kafka', name: 'kafka-clients', version: '0.9.0.0')
        compile(group: 'org.apache.kafka', name: 'kafka_2.10', version: '0.9.0.0')

        // statsd telemetry
        compile(group: 'com.datadoghq', name: 'java-dogstatsd-client', version: '2.1.1')
    }

    // lololololololololol (for JVMHackery)
    compileJava {
        options.compilerArgs << '-XDignore.symbol.file'
        options.fork = true
        options.forkOptions.executable = "javac"
    }

    // ditto
    javadoc {
        options.addStringOption('XDignore.symbol.file')
    }
}
